Bool {
    Program = Block

    // single line comments must exist on their own lines for now
    // multi line comments must either exist on their own or at the terminus
    // of a line
    Block
        = (FullStmt | singlecomment )*
    FullStmt
      = IndentStmt           --indent
      | NormalStmt? newline  --normal

    NormalStmt
      = VarDecl
      | OpAssign
      | VarAssignment
      | Exp
      | Return
      | breakstmt
      | FieldAssign
      | FieldDecl

    IndentStmt
      = ClassDecl
      | FunDecl
      | ObjDecl
      | Conditional
      | Loop
      // need to split loops into indent and no indent
    Stmt
      = ClassDecl
      | FunDecl
      | ObjDecl
      | VarDecl     -- vardecl
      | OpAssign
      | VarAssignment
      | Conditional
      | Loop        -- loop
      | Exp         -- exp
      | Return      -- ret

    ClassDecl
        = "class" id ("isa" id)? ":" ClassSuite

    ClassSuite
        = newline indent ClassBody dedent

    ClassBody
        = ConstructorDecl newline* (MethodDecl newline*)*

    FieldDecl
      = fieldid (Type)?
    FieldAssign
      = fieldexp "=" Exp
    fieldexp
      = fieldid

    fieldid
      = "_" id

    ConstructorDecl
      = "build" MethodParams ":" Suite --bodied
      | "build" FieldParams newline --noBody

    MethodDecl
      = id MethodParams ":" Suite

    MethodParams
      = "(" (ParamDecl | FieldDecl)* ")"
    FieldParams
      = "(" FieldDecl+ ")"
    // need to make this work where second newline not necessary before closing dedent
    ObjDecl
        = id ":=" newline indent (PropertyDecl newline*)+ dedent

    // may need to expand this
    PropertyDecl
      = VarExp ":" Exp

    FunDecl
      = "fun" id Type? Params ":" Suite

    VarDecl
      = "let" VarExp (Type)? ("=" Exp)?

    OpAssign
      = (id | fieldid) (assignop) Exp

    assignop
      = "+=" | "*=" | "/=" | "%="

    VarAssignment
      = VarExp "=" Exp

    Return = "ret" Exp?

    Funcall
      = (id | fieldid) Funparams+

    Funparams
      = "(" SimpleLit* ")"

    Params
      = "(" ParamDecl* ")"

    ParamDecl
      = id Type?

    Conditional
      = "if" Case ("elif" Case)* ("el" ":" Suite)?

    Case
      = Exp ":" Suite

    Suite
      = newline indent (FullStmt)+ dedent

    SimpleSuite
      = (newline indent)? Exp newline dedent

    Loop
        = "for" id "in" (Range | VarExp | Listlit ) ":" Suite     --forIn
        | "while" Exp ":" Suite                               --while

    Exp
      = Exp1 "or" Exp1      --binexp
      | Exp1
    Exp1
      = Exp2 "and" Exp2     --binexp
      | Exp2
    Exp2
      = Exp3 relop Exp3     --binexp
      | Exp3
    Exp3
      = Exp4 addop Exp4     --binexp
      | Exp4
    Exp4
      = Exp5 mulop Exp5     --binexp
      | Exp5
    Exp5
      = Exp6 expop Exp6     --expExp
      | Exp6
    Exp6
      = prefixop Exp7       --prefixOp
      | Exp7
    // may need to make this lexical
    Exp7
      = Exp8 "." (id | stringlit)                --access
      | Exp8 "[" (id | intlit | stringlit) "]"   --listAccess
      | Exp8
    Exp8
      =
      | Funlit
      | "(" Exp ")"   --parens
      | SimpleLit
      | Objlit
      | Listlit
      | ClassInst

    // need to add support for single line objects
    // May also want support for lists
    SimpleLit
      = boollit
      | floatlit
      | intlit
      | Funcall
      | VarExp
      | fieldexp
      | stringlit

    VarExp = id
    addop = "+" | "-"
    mulop = "*" | "/" | "%"
    prefixop = "!" | "-"
    expop = "**" | "//"
    relop =  ">=" | "<=" | ">" | "<" | "!==" | "===" | "==" | "!="
    Explist = Exp8 ("," Exp8)*
    ListExp = Exp "for" id "in" (VarExp | Listlit | fieldexp) SimpleIf? // TODO: add in rangeexp
    SimpleIf = "if" Exp
    Listlit = "[" (ListExp | Explist)? "]"
    Range = "range(" RangeExp ")"
    RangeExp = intlit ("," intlit("," "-"? intlit)?)?
    boollit = "tru" | "fal"
    intlit = ~(digit* ".") digit+
    floatlit = digit+ "." (digit+)? | (digit+)? "." digit+
    stringlit = "\"" (char | doublequotechar)* "\"" | "'" (char | singlequotechar)* "'"
    ClassInst = "new" id Funparams
    breakstmt = "break"
    Objlit
      = "{" PropertyDecl? "}" --singleprop
      | "{" newline indent (PropertyDecl newline)* dedent "}" --multiprop

    // should also allow for one line statement here
    Funlit = "lambda" Params ":" (Suite | Exp)

    reserved
      = "tru" | "fal" | "fun" | "if" | "elif" | "el" | "for" | "do"
      | "while" | "until" | "build" | "class" | "isa" | "in" | "new" | "#" | "ret"
      | "indent" | "dedent" | "[" | "]" | "let" | "string" | "float" | "int"
      | "bool" | "obj" | "⇦" | "⇨" | "break" | addop | mulop | expop
      | relop
    Type
      = ":" ("string" | "float" | "int" | "bool" | "obj" | id) --single
      | ":" "[" ("string" | "float" | "int" | "bool" | "obj" | id) "]" --list
    char
         = alnum | punctuation | escape | stringSpace

    doublequotechar = "'"
    singlequotechar = "\""

    stringSpace
         = " " | "\t" | "\n"

    punctuation
         = "." | "," | "/" | "<" | ">" | "?" | ":" | ";" | "{" | "}" | "["
         | "]" | "|" | "+" | "=" | "-" | "_" | "(" | ")" | "!" | "@" | "#"
         | "$" | "%" | "^" | "&" | "*"

    escape = "\\" // still need to fully define these
    comment
      = singlecomment
      | multicomment
    multicomment = "##" (~"##" any)* "##"
    singlecomment =  "#" ~"#" (~"\n" any)* "\n"

    indent = "⇨" // placeholder
    dedent = "⇦" // placeholder
    id = ~("_"? reserved ~(idChar+)) idName
    idName = idStart (idChar)*
    idStart = letter | "$"
    idChar = alnum | "-" | "_"
    newline = "\n" | "\r" | "\u2028" | "\u2029"
    // may need to amend this rule
    space := " " | "\t" | multicomment
}
